# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/hiviewdfx/hilog/hilog.gni")
import("//build/ohos.gni")

config("libhilog_pub_config") {
  visibility = [ ":*" ]
  include_dirs = [ "include" ]
}

template("libhilog") {
  forward_variables_from(invoker, "*")
  ohos_shared_library(target_name) {
    public_configs = [ ":libhilog_pub_config" ]

    deps = [
      "//base/hiviewdfx/hilog/frameworks/libhilog:libhilog_source_$platform",
    ]

    if (platform == "ohos") {
      output_extension = "so"
    }

    install_enable = !hilog_native_feature_ohcore
    install_images = [
      "system",
      "updater",
    ]
    part_name = "hilog_native"
    subsystem_name = "hiviewdfx"
  }
}

foreach(item, platforms) {
  if (item == "ohos") {
    libhilog("libhilog") {
      platform = item
    }
  }
  if (item == "windows" || item == "mac" || item == "linux") {
    libhilog("libhilog_" + item) {
      platform = item
    }
  }
}

config("libhilog_base_pub_cfg") {
  visibility = [ "*:*" ]
  include_dirs = [ "include" ]
}

ohos_static_library("libhilog_base") {
  public_configs = [ ":libhilog_base_pub_cfg" ]

  deps = [ "//base/hiviewdfx/hilog/frameworks/libhilog:libhilog_base_source" ]

  subsystem_name = "hiviewdfx"
  part_name = "hilog_native"
}
